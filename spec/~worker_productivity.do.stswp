do "../src/globals.do"

local specname      "worker_productivity"
local result_path   "$results/`specname'"
capture mkdir "`result_path'"

use "$processed_data/worker_panel.dta", clear

local outcomes total_contributions_q100 restricted_contributions_q100

*----------------------------------------------------------
* 2)  Output files & "first-time" flags
*----------------------------------------------------------
local ols_file  "`result_path'/ols_results.tex"
local iv_file   "`result_path'/iv_results.tex"
local fs_file   "`result_path'/first_stage.tex"

local first_ols 1
local first_iv  1
local fs_done   0          // 0 = not yet written

*----------------------------------------------------------
* 3)  LOOP over outcomes
*----------------------------------------------------------
foreach outcome of local outcomes {

    di as text "Processing outcome: `outcome'"

    *****************************************************
    * ----------  OLS
    *****************************************************
    reghdfe `outcome' var3 var5 var4, ///
        absorb(user_id firm_id yh) vce(cluster user_id)

    quietly summarize `outcome' if covid == 0
    local precovid_mean = r(mean)

	outreg2 using results.csv, replace csv
    outreg2 using "`ols_file'", tex(frag) ///
        `=cond(`first_ols', "replace", "append")' ///
        addstat("Pre-COVID Y-Mean", `precovid_mean')
    local first_ols 0


    *****************************************************
    * ----------  IV 
    *****************************************************
    ivreghdfe `outcome' (var3 var5 = var6 var7) var4, ///
        absorb(user_id firm_id yh) vce(cluster user_id) savefirst

    quietly summarize `outcome' if covid == 0
    local precovid_mean = r(mean)
    local rkf           = e(rkf)

    outreg2 using "`iv_file'", tex(frag) ///
        `=cond(`first_iv', "replace", "append")' ///
        addstat("Pre-COVID Y-Mean", `precovid_mean', ///
                "KP rk Wald F",      `rkf')
    local first_iv 0


	*****************************************************
	* ----------  FIRST-STAGE table  
	*****************************************************
	if !`fs_done' {

		* grab stacked F-stats before restoring
		matrix FS = e(first)
		local partialF_3 = FS[4,1]
		local partialF_5 = FS[4,2]

		* ---- var3 first stage ----
		estimates restore _ivreg2_var3
		outreg2 using "`fs_file'", tex(frag) replace ///
			addstat("Partial F", `partialF_3', "KP rk Wald F", `rkf')

		* ---- var5 first stage ----
		estimates restore _ivreg2_var5
		outreg2 using "`fs_file'", tex(frag) append  ///  
			addstat("Partial F", `partialF_5', "KP rk Wald F", `rkf')

		local fs_done 1
	}

}
